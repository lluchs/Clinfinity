/*  Script: Isle Respawn
	Provides functionality for saving an island and later respawning it or even copying it to other places.
	
	Island Save Format
	==================
	
	The island is saved using an array with the following structure:
	 - First element: Array saving the rectangle passed to <SaveIsland>
	 - Second element: Array of arrays of the format [material, number].
	   They express a horizontal string of material pixels of one type.
	   "Line breaks" have to be figured out using the first element. */

#strict 2

/*  Function: SaveIsland
	Saves and returns the island specified by the parameters.

	Parameters:
	x - Top-left point of the saving rectangle
	y - Top-left point of the saving rectangle
	wdt - Width of the saving rectangle
	hgt - Height of the saving rectangle

	Returns:
	The island data. */
global func SaveIsland(int x, int y, int wdt, int hgt) {
	var isle = [[x, y, wdt, hgt], []];
	var i = 1, mat = GetMaterial(x, y), n = 0, next;
	for(var iy = y; iy < y + hgt; iy++) {
		for(var ix = x; ix < x + wdt; ix++) {
			next = GetMaterial(ix, iy);
			if(next == mat)
				n++;
			else {
				PushBack([mat, n], isle[1]);
				mat = next;
				n = 1;
			}
		}
	}
	PushBack([mat, n], isle[1]);
	return isle;
}

/*  Function: RestoreIsland
	Restores a saved island instantly.

	*Warning*: This function might freeze the game for a short moment if the island is very large.

	The x and y parameters can be used to copy the saved island to other places.

	Parameters:
	isle - The island data generated by <SaveIsland>
	ox - (optional) A new x coordinate overwriting the original one
	oy - (optional) A new y coordinate overwriting the original one */
global func RestoreIsland(array isle, int ox, int oy) {
	var x = ox || isle[0][0],
	    y = oy || isle[0][1],
	    wdt = isle[0][2],
	    hgt = isle[0][3];
	
	var ix = x, iy = y;
	var length, remaining, n, yInc;
	for(var ms in isle[1]) {
		length = ms[1];
		while(length > 0) {
			// remaining pixels in this line
			remaining = wdt - ix + x;
			n = length;
			yInc = false;
			if(length >= remaining) {
				n = remaining;
				yInc = true;
			}
			if(ms[0] != -1) {
				DrawMaterialHLine(Format("%s-Smooth", MaterialName(ms[0])), ix, iy, n, true);
				CreateParticle("PSpark", ix, iy, 20, 5, 50, GetMaterialColorRGB(ms[0]));
			}
			ix += n;
			length -= n;
			if(yInc) {
				ix = x;
				iy++;
			}
		}
	}
	return true;
}

/*  Function: PeriodicIslandRespawn
	Saves and restores the given island periodically.

	The island won't be restored while there are Clonks inside.

	Parameters:
	interval - Time interval in which the specified island is restored
	other parameters - see <SaveIsland> */
global func PeriodicIslandRespawn(int interval, int x, int y, int wdt, int hgt) {
	var effectNum = AddEffect("PeriodicIslandRespawn", 0, 1, interval);
	EffectVar(0, 0, effectNum) = SaveIsland(x, y, wdt, hgt);
}

global func FxPeriodicIslandRespawnTimer(object target, int effectNum, int effectTime) {
	var isle = EffectVar(0, target, effectNum), rect = isle[0];
	if(!FindObject2(Find_OCF(OCF_Alive), Find_InRect(rect[0], rect[1], rect[2], rect[3]))) {
		RestoreIsland(isle);
	}
}
